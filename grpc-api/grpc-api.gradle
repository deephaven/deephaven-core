plugins {
    id 'java'
}

configurations {
    compile.extendsFrom dhDb
    testCompile.extendsFrom dhDbTest

    // custom configuration, sourceset, test task to keep flight-core off the main/test classpath
    flightTestCompile.extendsFrom(testCompile)
}

sourceSets {

    flightTest {
        java {
            srcDir 'src/flightTest/java'
        }
        annotationProcessorPath = sourceSets.test.annotationProcessorPath
        compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.flightTestCompile
        runtimeClasspath = sourceSets.main.output + sourceSets.test.output + configurations.testRuntimeClasspath + configurations.flightTestCompile + sourceSets.flightTest.output
    }
}

dependencies {
    compile project(':DB'),
            project(':Util'),
            project(':java-client-barrage-dagger'),
            project(':proto:proto-backplane-grpc-flight'),
            project(':open-api-lang-tools'),
            project(':log-factory'),
            project(':application-mode')

    Classpaths.inheritFlatbuffer(project, 'compile')

    Classpaths.inheritDagger(project)
    Classpaths.inheritDagger(project, /* test */ true)

    compile project(':Plot')

    compile project(':ClientSupport')

    if ('true' == project.findProperty('extensions.classgraph.enabled')) {
        runtimeOnly project(':extensions-classgraph')
    }
    if ('true' == project.findProperty('extensions.suanshu.enabled')) {
        runtimeOnly project(':extensions-suanshu')
    }

    testCompile TestTools.projectDependency(project, 'DB'),
                TestTools.projectDependency(project, 'Util')

    runtimeOnly project(':Numerics'), project(':Integrations'), project(':Kafka')
    Classpaths.inheritAssertJ(project)

    testRuntimeOnly project(':log-to-slf4j')
    Classpaths.inheritSlf4j(project, 'slf4j-simple', 'testRuntimeOnly')

    // it is essential that this is only added to the classpath for flightTestCompile
    Classpaths.inheritArrow(project, 'flight-grpc', 'flightTestCompile')
}

// testOutOfBand: non-parallel, not a db test, not isolated
TestTools.addDbTest(project, 'OutOfBand', false, false, false)

check.dependsOn(tasks.register('flightTest', Test) {
    classpath = sourceSets.flightTest.runtimeClasspath
    testClassesDirs = sourceSets.flightTest.output.classesDirs
    systemProperty "LiveTableMonitor.allowUnitTestMode", false
})

apply plugin: 'io.deephaven.java-open-nio'
