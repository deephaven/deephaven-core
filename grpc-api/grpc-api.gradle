plugins {
    id 'java'
}

configurations {
    // custom configuration, sourceset, test task to keep flight-core off the main/test classpath
    flightTestCompile.extendsFrom(testCompile)
}

sourceSets {

    flightTest {
        java {
            srcDir 'src/flightTest/java'
        }
        annotationProcessorPath = sourceSets.test.annotationProcessorPath
        compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.flightTestCompile
        runtimeClasspath = sourceSets.main.output + sourceSets.test.output + configurations.testRuntimeClasspath + configurations.flightTestCompile + sourceSets.flightTest.output
    }
}

dependencies {
    compile project(':engine-table'),
            project(':extensions-csv'),
            project(':extensions-parquet-table'),
            project(':Util'),
            project(':java-client-barrage-dagger'),
            project(':proto:proto-backplane-grpc-flight'),
            project(':open-api-lang-tools'),
            project(':log-factory'),
            project(':application-mode'),
            'com.github.f4b6a3:uuid-creator:3.6.0'

    Classpaths.inheritFlatbuffer(project, 'compile')

    Classpaths.inheritDagger(project)
    Classpaths.inheritDagger(project, /* test */ true)

    compile project(':Plot')

    compile project(':ClientSupport')

    if ('true' == project.findProperty('extensions.classgraph.enabled')) {
        runtimeOnly project(':extensions-classgraph')
    }
    if ('true' == project.findProperty('extensions.suanshu.enabled')) {
        runtimeOnly project(':extensions-suanshu')
    }

    testCompile TestTools.projectDependency(project, 'engine-table'),
                TestTools.projectDependency(project, 'Util')

    runtimeOnly project(':Numerics'),
            project(':Integrations'),
            project(':extensions-kafka')
    Classpaths.inheritAssertJ(project)

    testRuntimeOnly project(':log-to-slf4j')
    Classpaths.inheritSlf4j(project, 'slf4j-simple', 'testRuntimeOnly')

    // it is essential that this is only added to the classpath for flightTestCompile
    Classpaths.inheritArrow(project, 'flight-grpc', 'flightTestCompile')
}

// testOutOfBand: non-parallel, not a engine test, not isolated
TestTools.addEngineTest(project, 'OutOfBand', false, false, false)

check.dependsOn(tasks.register('flightTest', Test) {
    classpath = sourceSets.flightTest.runtimeClasspath
    testClassesDirs = sourceSets.flightTest.output.classesDirs
    systemProperty "UpdateGraphProcessor.allowUnitTestMode", false
})

apply plugin: 'io.deephaven.java-open-nio'
