
configurations {
    testImplementation.extendsFrom junit
}

// for now, all gwt testing will be manual, since we need to have full
// integration testing w/ running servers to be able to use selenium
// (and htmlunit has a bug where it cannot handle Promises).
// This currently defaults to true, and we'll change the default later,
// once we can hook up the rest of the integration testing framework.
boolean manualGwt = findProperty('gwtMode') != 'auto'
boolean testPort = findProperty('gwtTestPort')
String testServer = findProperty('dhTestServer') ?: 'ws://localhost:8123/socket'
String testDir = "$buildDir/testGwt"
task 'gwtTest', type: Test, {
    Test t ->
        t.inputs.files(sourceSets.test.output.files)
//        t.classpath = configurations.testRuntime
        t.systemProperties = [ 'gwt.args': "${manualGwt ? '-runStyle Manual:1' : ''} ${testPort ? /-port $testPort/ : ''} -war $testDir/war -setProperty dhTestServer=$testServer -ea -style PRETTY -generateJsInteropExports",
                               'gwt.persistentunitcachedir': "$testDir/unitCache",
                               'dhTestServer': testServer
        ]
        t.include '**/*TestSuite.class'
        t.useJUnit()
        t.maxHeapSize = '2G'
        t.scanForTestClasses = false
        // never mark task as uptodate when using manual mode
        t.outputs.upToDateWhen { !manualGwt }
}

test {
    exclude '**/*TestGwt.class', '**/*TestSuite.class'
}
