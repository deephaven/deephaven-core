apply plugin: 'base'

JavaVersion currentJavaVersion = JavaVersion.current()
boolean is8 = currentJavaVersion.isJava8()

ext {
    // changing groovy version is dangerous for customer PQ stability; only upgrade is user is also upgrading java version >8
    GROOVY_VERSION = is8 ? '2.3.6' : '2.5.10'
}

configurations {

    commonsIo
    commonsCsv.extendsFrom commonsIo
    formsRt.extendsFrom commonsLang
    getDown.extendsFrom formsRt, commonsIo
    jdom
    httpClient
    math3
    packer
    infoNode
    jama.extendsFrom math3, commonsCli
    dxCompile
    dxRuntime.extendsFrom dxCompile
    mockrunner
    mxj
    junit

    fishBase
    fishIo.extendsFrom fishBase
    fishDataStructure.extendsFrom fishIo
    fishConfig.extendsFrom fishDataStructure
    fishDataGenerator.extendsFrom jdom
    fishNet.extendsFrom fishIo
    fishData.extendsFrom fishConfig, fishDataGenerator, jdom
    fishNumerics.extendsFrom fishBase
    fishUtil.extendsFrom fishConfig
    fishStats.extendsFrom fishUtil, fishData
    fishBaseTest.extendsFrom junit
    fishIoTest.extendsFrom fishBaseTest
    fishDataTest.extendsFrom fishIoTest

    dhNumerics.extendsFrom fishNumerics, jama
    dhUtil.extendsFrom commonsCli, commonsIo, commonsCsv, commonsLang3, commonsText, fishUtil, fishStats, jdom
    dhDb.extendsFrom fishStats, fishNumerics, jdom, commonsCsv, commonsCli, commonsLang3, jacksonDatabind, slf4jSimple
    dhDbTest.extendsFrom dhDb, mxj, fishDataTest
    dhPlot.extendsFrom dhDb
    dhDbTypes.extendsFrom fishBase, commonsIo
    dhDbTypesImpl.extendsFrom fishBase, commonsIo
    dhBenchmarkSupport.extendsFrom fishData, commonsCsv
    dhIntegrations.extendsFrom math3
    dhKafka.extendsFrom dhDb
}

// exported properties
ext {
    includeFish = project.hasProperty("includeFish") && project.includeFish == 'true'
}

dependencies {

    // First, one-off configurations for stuff we need "here and there"
    jdom 'org.jdom:jdom2:2.0.6'
    httpClient 'org.apache.httpcomponents:httpclient:4.5.6'
    packer 'packer:packer:1.0'
    commonsLang3 'org.apache.commons:commons-lang3:3.9'
    commonsText 'org.apache.commons:commons-text:1.9'
    formsRt 'com.intellij:forms_rt:6.0.3'
    commonsIo 'commons-io:commons-io:2.6'
    commonsCli 'commons-cli:commons-cli:1.4'
    math3 'org.apache.commons:commons-math3:3.6.1'
    jama 'gov.nist.math.jama:gov.nist.math.jama:1.1.1'
    infoNode 'io.deephaven.thirdparty:infonode:1.8.3'
    mockrunner 'com.mockrunner:mockrunner-jdbc:1.0.4'
    commonsCsv 'org.apache.commons:commons-csv:1.4'
    mxj 'mysql:mysql-connector-mxj:5.0.12'
    junit fileTree(dir: "${rootDir}/test-libs", include: ['*.jar'])
    jacksonDatabind 'com.fasterxml.jackson.core:jackson-databind:2.10.1'
    slf4jSimple 'org.slf4j:slf4j-simple:1.7.30'

    // Now, all the various fish libs we depend on, with inherited dependencies matching
    // the actual dependencies used in fishlib... if we were strongly coupled, we could skip
    // having to maintain this...
    fishBase project(':Base'),
            'log4j:log4j:1.2.16',
            'net.sf.trove4j:trove4j:3.0.3',
            'com.intellij:annotations:5.1',
            'commons-codec:commons-codec:1.11',
            'org.apache.commons:commons-compress:1.18'

    fishIo project(':IO')

    fishDataStructure project(':DataStructures')

    fishConfig project(':Configuration')

    fishUtil project(':FishUtil')

    fishStats project(':Net'),
            project(':Stats'),
            'com.google.code.java-allocation-instrumenter:java-allocation-instrumenter:2.0'

    fishBaseTest project(path: ':Base', configuration: 'tests')

    fishIoTest project(path: ':IO', configuration: 'tests')

    // Now, the good stuff...  all dh classpaths

    dhUtil 'ant:ant:1.6.5'

    dhDb 'joda-time:joda-time:2.10',
            "org.codehaus.groovy:groovy:$GROOVY_VERSION",
            "org.codehaus.groovy:groovy-json:$GROOVY_VERSION",
            'com.jcraft:jsch:0.1.51',
            "org.eclipse.jgit:org.eclipse.jgit:$Classpaths.JGIT_VERSION",
            "org.eclipse.jgit:org.eclipse.jgit.ssh.jsch:$Classpaths.JGIT_VERSION",
            'org.scala-lang:scala-library-all:2.12.2',
            'org.scala-lang:scala-compiler:2.12.2',
            'org.yaml:snakeyaml:1.22',
            'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.8',
            'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.9.8',
            'com.github.javaparser:javaparser-core:2.0.0',
            'com.tdunning:t-digest:3.2'

    // https://mvnrepository.com/artifact/com.univocity/univocity-parsers
    dhBenchmarkSupport 'com.univocity:univocity-parsers:2.6.0',
            // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
            'com.fasterxml.jackson.core:jackson-core:2.9.8'

    dhKafka 'org.apache.kafka:kafka-clients:2.4.0',
            'org.apache.avro:avro:1.9.2'
}
