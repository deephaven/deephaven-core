import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import io.deephaven.tools.License

plugins {
    id 'com.bmuschko.docker-remote-api'
    id 'io.deephaven.project.register'
}

evaluationDependsOn Docker.registryProject('envoyproxy')

def dockerLicenses = License.createFrom(project).syncDockerLicense()

def prepareDocker = project.tasks.register('prepareDocker', Sync) {
    // TODO(deephaven-core#1596): Remove extra dependencies for build-ci.yml
    inputs.files Docker.registryFiles(project, 'envoyproxy')

    from 'Dockerfile'
    from ('contents') {
        into 'contents'
    }
    from (dockerLicenses.get().outputs) {
        into 'contents'
    }
    into 'build/docker'
}

def buildDocker = Docker.registerDockerImage(project, 'buildDocker') {
    inputs.files prepareDocker.get().outputs.files
    inputs.files Docker.registryFiles(project, 'envoyproxy')
    buildArgs.put('DEEPHAVEN_VERSION', project.version)
    images.add('deephaven/envoy:local-build')
}

assemble.dependsOn buildDocker