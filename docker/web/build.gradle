import io.deephaven.tools.License

plugins {
    id 'com.bmuschko.docker-remote-api'
}

configurations {
    js
}
dependencies {
    js project(path: ':web', targetConfiguration: 'js')
}

evaluationDependsOn Docker.registryProject('nginx-base')

def dockerLicenses = License.createFrom(project).syncDockerLicense()

def syncDir = layout.buildDirectory.dir('docker')
def prepareDocker = project.tasks.register('prepareDocker', Sync) {
    // TODO(deephaven-core#1596): Remove extra dependencies for build-ci.yml
    // Currently, GH build-ci.yml calls dockerCreateDockerfile, and relies on buildx; so we need to
    // make sure that dockerCreateDockerfile has the proper dependencies.
    // If we change how our build works in the future (so that gradle is responsible for pushing the
    // image), we can remove this extra dependency.
    dependsOn Docker.registryTask(project, 'nginx-base')

    from layout.projectDirectory.dir('src/main/docker')
    from(configurations.js) {
        into 'static'
    }
    from (dockerLicenses.get().outputs) {
        into 'licenses'
    }
    into syncDir
}

Docker.registerDockerImage(project, 'buildDocker') {
    inputDir.set syncDir
    inputs.files Docker.registryFiles(project, 'nginx-base')
    inputs.files prepareDocker.get().outputs.files
    buildArgs.put('DEEPHAVEN_VERSION', project.version)
    images.add('deephaven/web:local-build')
}

assemble.dependsOn buildDocker
