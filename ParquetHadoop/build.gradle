plugins {
    id 'java-library'
    id 'io.deephaven.project.register'
}

sourceSets {
    main {
        java {
            srcDir 'java'
        }
    }
}

dependencies {
    api('org.apache.parquet:parquet-hadoop:1.12.0')

    api('org.apache.hadoop:hadoop-common:3.3.1') {
        // do not take any dependencies of this project,
        // we just want a few classes (Configuration, Path) for
        // simplified prototyping work, and api compatibility.
        transitive = false
        // if we actually need any more of hadoop at runtime,
        // we can add more jars w/ transitive=false,
        // or replace transitive=false here w/ more exclusions;
        // (we want to avoid pulling in netty, loggers, jetty-util, guice and asm).

    }

    /* A dependency to woodstox is triggered from the
     *  initialization of ParquetReader the first time
     *  we try to open a parquet file.  Without this below,
     *  we get:
     *
     *  java.lang.NoClassDefFoundError: com/ctc/wstx/io/InputBootstrapper
     *    at io.deephaven.parquet.ParquetFileReader.lambda$new$0(ParquetFileReader.java:44)
     *    at java.lang.ThreadLocal$SuppliedThreadLocal.initialValue(ThreadLocal.java:284)
     *    at java.lang.ThreadLocal.setInitialValue(ThreadLocal.java:180)
     *    at java.lang.ThreadLocal.get(ThreadLocal.java:170)
     *    at io.deephaven.parquet.ColumnChunkReaderImpl.lambda$new$0(ColumnChunkReaderImpl.java:49)
     *    [...]
     *
     * Similarly for hadoop-shaded-guave.
     */
    runtimeOnly('com.fasterxml.woodstox:woodstox-core:5.2.1')
    runtimeOnly('org.apache.hadoop.thirdparty:hadoop-shaded-guava:1.1.1')
    // ParquetFileWriter triggers a dependency to UnmodifieableMap via hadoop.conf.Configuration.
    runtimeOnly 'commons-collections:commons-collections:3.2.2'
    // These are necessary for parquet compression codec support.
    runtimeOnly('com.hadoop.gplcompression:hadoop-lzo:0.4.20')  // For LZO codec.
    runtimeOnly('org.lz4:lz4-java:1.8.0')                       // For LZ4 codec.
    runtimeOnly('com.github.rdblue:brotli-codec:0.1.1')         // For Brotli codec.
}
