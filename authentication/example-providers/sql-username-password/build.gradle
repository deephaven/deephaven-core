configurations {
    pwGen
}
dependencies {
    implementation('org.postgresql:postgresql:42.4.0') {
        because 'Postgres driver to connect to a running DB to query for user information'
    }
    implementation('org.mindrot:jbcrypt:0.4') {
        because 'bcrypt is used in this example to ensure passwords are never stored or queried in plain text'
    }

    pwGen project(':log-to-stream')
    Classpaths.inheritLogbackClassic(project, 'pwGen')
}

shadowJar {
    relocate 'org', 'io.deephaven.authentication.sql.rebased'
}

// Usage:
// ./gradlew :authentication:example-providers:sql-username-password:generatePassword --args=password
tasks.register('generatePassword', JavaExec) {
    classpath(configurations.shadow.files)
    classpath(configurations.pwGen.files)
    classpath(sourceSets.main.runtimeClasspath)
    mainClass = 'io.deephaven.authentication.sql.BasicSqlAuthenticationHandler'
    systemProperty 'Configuration.rootFile', 'lib-tests.prop'
    systemProperty 'authentication.basic.sql.jdbc.connection', 'not used'
}