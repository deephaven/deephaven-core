plugins {
    id 'io.deephaven.project.register'
}

project.subprojects { Project p ->
    p.plugins.apply('java-library')

    // Combined, these next two lines will activate the JAVA_PUBLIC_SHADOW to all
    // sub-projects, and in turn, apply the shadow plugin
    p.setProperty('io.deephaven.project.ProjectType', 'JAVA_PUBLIC_SHADOW')
    p.plugins.apply('io.deephaven.project.register')

    p.dependencies {
        // Standard dependencies all providers will need, but should not be shadowed
        shadow project(':authentication')
        shadow project(':Configuration')
        shadow project(':log-factory')
    }

    // ensure we build the shadow jar as part of simple builds
    p.tasks.findByName('assemble').dependsOn(p.tasks.named('shadowJar'))

    // Let projects be named consistently
    p.base.archivesName = "deephaven-${p.name}-authentication-provider"
}
