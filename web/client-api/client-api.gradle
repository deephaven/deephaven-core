plugins {
    id 'io.deephaven.project.register'
}

apply from: "$rootDir/gradle/web-client.gradle"

configurations {
    js
}

dependencies {
    implementation project(':web-shared-beans')
    implementation project(':web-client-backplane')

    implementation 'com.vertispan.tsdefs:jsinterop-ts-defs-annotations:HEAD-SNAPSHOT'
    annotationProcessor 'com.vertispan.tsdefs:jsinterop-ts-defs-processor:HEAD-SNAPSHOT'

    implementation 'com.vertispan.nio:gwt-nio:1.0-alpha-1'

    js project(path: ':proto:raw-js-openapi', configuration: 'js')
}
Classpaths.inheritElemental(project, 'elemental2-core', 'implementation')
Classpaths.inheritElemental(project, 'elemental2-promise', 'implementation')
Classpaths.inheritElemental(project, 'elemental2-dom', 'implementation')

GwtTools.gwtCompile project, 'io.deephaven.web.DeephavenApi', 'Create a jar of client JS API'

def jsOutput = layout.buildDirectory.dir('js')
def gwtOutput = tasks.register('gwtOutput', Sync) {
    includeEmptyDirs = false
    from(tasks.getByName('gwtCompile').outputs.files) {
        // only copy the dhapi module, and don't give it a wrapper directory
        include 'dhapi/**'
        eachFile { it.path = 'jsapi/' + it.path.substring('dhapi/'.length()) }
    }
    into jsOutput
}

artifacts {
    js(jsOutput) {
        builtBy gwtOutput
    }
}

project.tasks.getByName('quick').dependsOn project.tasks.withType(de.esoco.gwt.gradle.task.GwtCompileTask)

apply from: "$rootDir/gradle/web-gwt-test.gradle"
