plugins {
    id 'com.bmuschko.docker-remote-api'
    id 'io.deephaven.project.register'
}

configurations {
    dts
}
dependencies {
    dts project(path: ':web-client-api', configuration: 'dts')
}

Docker.registerDockerTask(project, 'typedoc') {
    copyIn {
        from(configurations.dts) {
            into 'dist'
        }
        from 'tsconfig.json'
        from 'package.json'
        from 'package-lock.json'

        from('src/main/docker') {
            include 'theme.css'
        }
    }
    dockerfile {
        // share the common base image to keep it simple
        from 'deephaven/node:local-build'

        copyFile('.', '/project')

        runCommand('''set -eux; \\
                      cd /project/; \\
                      mv dist/types.d.ts dist/index.d.ts; \\
                      npm ci; \\
                      npm pack; \\
                      mkdir /out; \\
                      mv deephaven-jsapi-types*.tgz /out/; \\
                      node_modules/.bin/typedoc dist/index.d.ts \\
                           --out /out/documentation \\
                           --skipErrorChecking \\
                           --hideGenerator \\
                           --disableSources \\
                           --customCss theme.css; \\
                      ''')
    }
    parentContainers = [ Docker.registryTask(project, 'node') ] // deephaven/node
    containerOutPath = '/out'
    copyOut {
        into "$buildDir/"
    }
}
