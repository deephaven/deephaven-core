import io.deephaven.tools.License

plugins {
    id 'com.bmuschko.docker-remote-api'
}

evaluationDependsOn ':web-client-ui'
evaluationDependsOn ':proto:raw-js-openapi'

apply from: "$rootDir/gradle/web-client.gradle"

dependencies {
    compile project(':web-client-api')
    compile project(':open-api-lang-parser')
}

GwtTools.gwtCompile project, 'io.deephaven.ide.DeephavenIde', 'Create a jar of javascript for web IDE'

// deephaven/dhide
Task dhide = project(':web-client-ui').tasks.findByName('ideClientMakeImage')

// deephaven/js-out
Task jsOut = project(':proto:raw-js-openapi').tasks.findByName('webpackSourcesMakeImage')

Task gwtc = tasks.getByName('gwtCompile')

def dockerLicenses = License.createFrom(project).syncDockerLicense()

def prepareDocker = project.tasks.register('prepareDocker', Sync) {
    // Note: GH actions currently prepares docker via gradle, but does its own building via
    // a native GH action. As such, we need to make sure that the prepare also invokes the
    // necessarily build pre-reqs. If GH actions eventually calls buildDocker instead, we
    // can remove these dependencies.
    inputs.files([dhide, jsOut].each { t -> t.outputs.files })

    into "${buildDir}/docker"
    from(gwtc.outputs.files) {
        include("dhapi/**")
    }
    from (dockerLicenses.get().outputs) {
        into 'licenses'
    }
    from 'docker/Dockerfile'
    from 'nginx/default.conf'
    from 'nginx/nginx.conf'
    from 'nginx/99-init-notebooks.sh'
}

Docker.registerDockerImage(project, 'buildDocker') {
    inputs.files([prepareDocker.get(), dhide, jsOut].each { t -> t.outputs.files })
    //buildArgs.put('DEEPHAVEN_VERSION', "${project.version}")
    images.add('deephaven/web:local-build')
}

