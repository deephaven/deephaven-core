import com.bmuschko.gradle.docker.tasks.image.Dockerfile

plugins {
    id 'com.bmuschko.docker-remote-api'
    id 'io.deephaven.project.register'
}

description = 'Generates docs for the python libraries provided in Deephaven Core'

evaluationDependsOn ':docker-server-jetty'

configurations {
    pythonWheel
}
dependencies {
    pythonWheel project(':py-client')
}

def copyPyClientWhl = tasks.register('copyPyClientWhl', Sync) {
    from configurations.pythonWheel
    into layout.buildDirectory.file('sphinx-image/wheel/')
}

def copySphinxLib = tasks.register('copySphinxLib', Sync) {
    from "$rootDir/sphinx/lib/"
    into layout.buildDirectory.file('sphinx-image/lib')
}

def sphinxDockerfile = tasks.register('sphinxDockerfile', Dockerfile) {
    destFile.set layout.buildDirectory.file('sphinx-image/Dockerfile')
    // Deephaven server python API requires that the wheel be installed to build, so we share this image
    from Docker.localImageName('server-jetty')

    copyFile "./wheel", "/wheel"
    copyFile "./lib", '${VIRTUAL_ENV}/lib/python3.10/site-packages/'

    environmentVariable 'DEEPHAVEN_CLASSPATH', "/opt/deephaven/server/lib/*.jar"
    environmentVariable 'DEEPHAVEN_WORKSPACE', '/tmp/workspace'
    environmentVariable 'DEEPHAVEN_PROPFILE', 'dh-defaults.prop'
    environmentVariable 'DEEPHAVEN_VERSION', project.version

    // For the time being we lock the sphinx version to 7.1.2
    // This works around the bug described in
    // https://github.com/sphinx-doc/sphinx/issues/11605
    // We should leave this here until 'breathe' updates their code to be
    // compatible with the breaking sphinx change.
    runCommand '''set -eux; \\
                  mkdir /tmp/workspace; \\
                  python -m pip install sphinx==7.1.2 sphinx-autodoc-typehints pyarrow protobuf grpcio bitstring /wheel/*.whl breathe furo
                  '''
}

def sphinxImage = Docker.registerDockerImage(project, 'sphinx') {
    inputs.files sphinxDockerfile.get().outputs.files
    inputs.files copyPyClientWhl.get().outputs.files
    inputs.files copySphinxLib.get().outputs.files
    inputDir.set layout.buildDirectory.dir('sphinx-image')
    inputs.files project(':docker-server-jetty').tasks.findByName('buildDocker-server-jetty').outputs.files // deephaven/server-jetty
    images.add(Docker.localImageName('sphinx'))
}

def makePyDocTask = { name, archiveBaseName, sourcePaths, outDirPath, Closure c = {} ->

    TaskProvider<? extends Task> pyDoc = Docker.registerDockerTask(project, name) { cfg ->
        cfg.copyIn {
            sourcePaths.each { entry ->
                from(entry.key) {
                    into entry.value
                }
            }
        }
        cfg.dockerfile {
            from Docker.localImageName('sphinx')

            copyFile('.', '/project')

            runCommand '''set -eux; \\
                       mkdir /build; \\
                       cd /project; \\
                       sphinx-build -n -b html source /build
                       '''
        }
        cfg.parentContainers = [sphinxImage.get()]
        cfg.containerOutPath = '/build'
        cfg.copyOut {
            into outDirPath
        }

        if (c) c(cfg)
    }


    project.tasks.register "${name}Tar", Tar, {
        Tar tar ->
            tar.from(outDirPath)
            tar.dependsOn(pyDoc)
            tar.archiveBaseName.set(archiveBaseName)
    }
}

def cppClientDoxygenTask = Docker.registerDockerTask(project, 'cppClientDoxygen') {
    copyIn {
        from "$rootDir/cpp-client"
    }
    dockerfile {
        // share the common base image to keep it simple
        from Docker.localImageName('server-jetty')

        runCommand('''set -eux; \\
                      apt-get update; \\
                      apt-get -y install doxygen graphviz; \\
                      rm -rf /var/lib/apt/lists/*
                      ''')

        copyFile('.', '/project')

        runCommand('''set -eux; \\
                      cd /project/doc; \\
                      doxygen
                      ''')
    }
    parentContainers = [project(':docker-server-jetty').tasks.findByName('buildDocker-server-jetty')] // deephaven/server-jetty
    containerOutPath = '/project/doc/doxygenoutput'
    copyOut {
        into "$buildDir/cppClientDoxygen"
    }
}

def pythonDocs = makePyDocTask('pythonDocs', 'dh-python-docs', [("$projectDir/source".toString()):'source'], "$buildDir/docs")
//TODO why do docs depend on this, but the wheel itself doesn't? makes more sense to depend on assert, right?
project.tasks.named('pythonDocsPrepareDocker').configure {
    dependsOn ':Generators:generateFigureImmutable'
    dependsOn ':Generators:generatePythonFigureWrapper'
}

makePyDocTask('pydeephavenDocs', 'pydeephaven', [
        ("$rootDir/py/client/docs/source".toString()):'source',
        ("$rootDir/py/client/pydeephaven".toString()):'pydeephaven'
], "$buildDir/pyclient-docs")


def cppClientDocs = makePyDocTask('cppClientDocs', 'cpp-client', [
        ("$buildDir/cppClientDoxygen".toString()):'source/doxygenoutput',
        ("$rootDir/cpp-client/doc".toString()):'source',
        ("$rootDir/cpp-client/deephaven".toString()):'deephaven',
], "$buildDir/cppClientDocs")
tasks.named('cppClientDocsPrepareDocker').configure {
    dependsOn cppClientDoxygenTask
}

makePyDocTask('cppExamplesDocs', 'cpp-examples', [
        ("$rootDir/cpp-client/deephaven/examples/doc".toString()):'source',
        ("$buildDir/cppClientDocs".toString()):'cpp'
], "$buildDir/cppExamplesDocs")
tasks.named('cppExamplesDocsPrepareDocker').configure {
    dependsOn cppClientDocs
}