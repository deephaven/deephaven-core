import io.deephaven.tools.docker.DiffTask

plugins {
    id 'com.bmuschko.docker-remote-api'
    id 'io.deephaven.python-wheel'
    id 'io.deephaven.deephaven-in-docker'
}

evaluationDependsOn Docker.registryProject('python')

wheel {
    src 'pydeephaven'
    src 'deephaven_core'
    src 'examples'
    src 'tests'
}

configurations {
    python {}
}

dependencies {
    python project(path: ':proto:proto-backplane-grpc', configuration: 'python')
}

def compare = tasks.register('compareProtobuf', DiffTask) {
    expectedContents.set configurations.python
    actualContents {
        directory layout.projectDirectory
        ignore {
            it.exclude 'deephaven_core/proto/**'
        }
    }
    generateTask.set ':py-client:updateProto'

}
// fail a "check" build if these are out of date
tasks.getByName('quick').dependsOn(compare)

tasks.register('updateProtobuf', Sync) {
    finalizedBy compare
    from configurations.python
    into layout.projectDirectory
    preserve {
        exclude 'deephaven_core/proto/**'
    }
}

// Start up a docker container for the grpc server, then run pydeephaven test
String randomSuffix = UUID.randomUUID().toString();
deephavenDocker {
    envVars.set([
        'START_OPTS':'-Xmx512m -DAuthHandlers=io.deephaven.auth.AnonymousAuthenticationHandler'
    ])
    containerName.set "pydeephaven-test-container-${randomSuffix}"
    networkName.set "pydeephaven-network-${randomSuffix}"
}

def testPyClient = Docker.registerDockerTask(project, 'testPyClient') {
    copyIn {
        from('pydeephaven') {
            into 'project/pydeephaven'
        }
        from('deephaven_core') {
            into 'project/deephaven_core'
        }
        from('tests') {
            into 'project/tests'
        }
        from ('requirements-dev.txt') {
            into 'project/'
        }
        from ('setup.py') {
            into 'project/'
        }
        from ('README.md') {
            into 'project/'
        }
    }
    containerDependencies.dependsOn = [deephavenDocker.healthyTask]
    containerDependencies.finalizedBy = deephavenDocker.endTask
    network = deephavenDocker.networkName.get()
    dockerfile {
        from(Docker.localImageName('python'))
        copyFile('project', '/project')
        workingDir('/project')
        runCommand '''set -eux; \\
                      mkdir -p /out/report; \\
                      pip3 install --upgrade pip; \\
                      pip3 install -r requirements-dev.txt'''

        environmentVariable 'DEEPHAVEN_VERSION', project.version
        runCommand 'pip install .'
        environmentVariable 'DH_HOST', deephavenDocker.containerName.get()
        environmentVariable 'DH_PORT', '10000'
    }
    parentContainers = [ Docker.registryTask(project, 'python') ]
    entrypoint = ['python', '-m', 'xmlrunner', 'discover', 'tests', '-v', '-o', '/out/report']
    copyOut {
        into layout.buildDirectory.dir('test-results')
    }
}

tasks.getByName('check').dependsOn(testPyClient)
deephavenDocker.shouldLogIfTaskFails testPyClient
