plugins {
    id 'io.deephaven.project.register'
    id 'com.bmuschko.docker-remote-api'
}

evaluationDependsOn Docker.registryProject('python-dev-base')

configurations {
    pythonWheel
}

test {
    onlyIf { false } // don't run tests, it depends on python assets, which we aren't building ATM
    exclude "**/*" // make sure intellij doesn't try to use this disabled test class
}

dependencies {
    testCompile 'junit:junit:4.13.2' // but we still want tests to compile
}

// For DH Community, we need to currently move fast, and will prefer to make edits directly in py/jpy.
// These can be backported to jpy proper in the future if necessary.

def buildWheel = Docker.registerDockerTask(project, 'buildWheel') {
    copyIn {
        from(project.projectDir) {
            exclude 'build'
        }
    }
    parentContainers = [ Docker.registryTask(project, 'python-dev-base') ]
    buildArgs = [ DEEPHAVEN_VERSION: project.version.toString() ]
    imageName = 'deephaven/deephaven-jpy-wheel:local-build'
    containerOutPath = '/usr/src/app/dist'
    copyOut {
        into 'build/deephaven-jpy-wheel'
    }
}

artifacts {
    pythonWheel buildWheel
}
