plugins {
    id 'groovy'
    id 'io.deephaven.project.register'
}

apply from: "$rootDir/gradle/web-common.gradle"

Classpaths.inheritCommonsText(project)
dependencies {

    compile project(':open-api-shared-ide'),
            project(':open-api-lang-parser')

    compile project(':engine-table'),
            project(':engine-function'),
            project(':IO')

    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'

    Classpaths.inheritJUnitPlatform(project)
    testImplementation 'org.junit.jupiter:junit-jupiter'

    Classpaths.inheritGroovy(project, 'groovy', 'testCompile')
    Classpaths.inheritGroovy(project, 'groovy-templates', 'testCompile')

    testCompile TestTools.projectDependency(project, 'engine-table')
    testRuntime project(':configs')
    testRuntime project(':test-configs')
    testRuntime(project(':Numerics'))
    // intellij can be dumb and leave out our resources directory (sometimes)...
    // Do not cargo-cult this unless you have similar issues
    testRuntime files('build/resources/main')

    testRuntimeOnly project(':log-to-slf4j')
    Classpaths.inheritSlf4j(project, 'slf4j-simple', 'testRuntimeOnly')
}

test {
    systemProperty('Configuration.rootFile', 'dh-tests.prop')

    def python = System.getenv 'PYTHONPATH'
    environment('PYTHONPATH', (python?"$python$File.pathSeparator":'') +
            new File(project.rootDir, 'Integrations/python/deephaven').absolutePath)

    def pyProps = project.properties.subMap(["jpy.pythonLib", "jpy.jpyLib", "jpy.jdlLib"])
    systemProperties.putAll(pyProps)

    useJUnitPlatform()
}
