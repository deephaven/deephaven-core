plugins {
    id 'groovy'
    id 'io.deephaven.project.register'
    id 'java-library'
}

apply from: "$rootDir/gradle/web-common.gradle"

Classpaths.inheritCommonsText(project, 'implementation')
dependencies {

    implementation project(':open-api-lang-parser')
    implementation project(':engine-table')
    implementation project(':engine-function')
    implementation project(':IO')
    implementation project(':log-factory')

    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'

    Classpaths.inheritJUnitPlatform(project)
    testImplementation 'org.junit.jupiter:junit-jupiter'

    Classpaths.inheritGroovy(project, 'groovy', 'testImplementation')
    Classpaths.inheritGroovy(project, 'groovy-templates', 'testImplementation')

    testImplementation TestTools.projectDependency(project, 'engine-table')
    testRuntimeOnly project(':configs')
    testRuntimeOnly  project(':test-configs')
    testRuntimeOnly project(':Numerics')
    // intellij can be dumb and leave out our resources directory (sometimes)...
    // Do not cargo-cult this unless you have similar issues
    testRuntimeOnly files('build/resources/main')

    testRuntimeOnly project(':log-to-slf4j')
    Classpaths.inheritSlf4j(project, 'slf4j-simple', 'testRuntimeOnly')
}

test {
    systemProperty('Configuration.rootFile', 'dh-tests.prop')

    def python = System.getenv 'PYTHONPATH'
    environment('PYTHONPATH', (python?"$python$File.pathSeparator":'') +
            new File(project.rootDir, 'Integrations/python/deephaven').absolutePath)

    def pyProps = project.properties.subMap(["jpy.pythonLib", "jpy.jpyLib", "jpy.jdlLib"])
    systemProperties.putAll(pyProps)

    useJUnitPlatform()
}
