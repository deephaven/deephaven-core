plugins {
    id 'groovy'
    id 'io.deephaven.project.register'
    id 'java-library'
}

dependencies {
    implementation libs.commons.text

    implementation project(':open-api-lang-parser')
    implementation project(':engine-table')
    implementation project(':engine-function')
    implementation project(':IO')
    implementation project(':log-factory')

    testImplementation libs.spockframework.core

    testImplementation platform(libs.junit.bom)
    testImplementation libs.junit.jupiter

    testImplementation libs.groovy
    testImplementation libs.groovy.templates

    testImplementation project(':engine-test-utils')
    testRuntimeOnly project(':configs')
    testRuntimeOnly project(':test-configs')
    testRuntimeOnly project(':Numerics')
    // intellij can be dumb and leave out our resources directory (sometimes)...
    // Do not cargo-cult this unless you have similar issues
    testRuntimeOnly files('build/resources/main')

    testRuntimeOnly project(':log-to-slf4j')
    testRuntimeOnly libs.slf4j.simple
}

test {
    systemProperty('Configuration.rootFile', 'dh-tests.prop')

    def python = System.getenv 'PYTHONPATH'
    environment('PYTHONPATH', (python?"$python$File.pathSeparator":'') +
            new File(project.rootDir, 'Integrations/python/deephaven').absolutePath)

    def pyProps = project.properties.subMap(["jpy.pythonLib", "jpy.jpyLib", "jpy.jdlLib"])
    systemProperties.putAll(pyProps)

    useJUnitPlatform()
}
