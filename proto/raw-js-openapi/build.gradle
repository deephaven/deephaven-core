plugins {
    id 'com.bmuschko.docker-remote-api'
    id 'io.deephaven.project.register'
}

evaluationDependsOn ':proto'
evaluationDependsOn ':proto:proto-backplane-grpc'
evaluationDependsOn Docker.registryProject('node')

def backplaneProject = project(':proto:proto-backplane-grpc')
def webpackSourcesLocation = layout.buildDirectory.dir("${buildDir}/dhapi")

Docker.registerDockerTask(project, 'webpackSources') {
    copyIn {
        from(backplaneProject.tasks.findByName('generateProtobuf').outputs.files) {
            // note: we are only copying the JS and not TS files.
            include '**/*.js'
            into 'raw-js-openapi/build/js-src'
        }

        from('src') {
            into 'raw-js-openapi/src'
        }

        from ('webpack.config.js') {
            into 'raw-js-openapi/'
        }
        from ('tsconfig.json') {
            into 'raw-js-openapi/'
        }
        from ('Dockerfile')
        from ('../package.json')
        from ('../package-lock.json')
    }
    parentContainers = [ Docker.registryTask(project, 'node') ]
    imageName = 'deephaven/js-out:local-build'
    containerOutPath = '/usr/src/app/raw-js-openapi/build/js-out'
    copyOut {
        include 'dh-internal.js'
        into(webpackSourcesLocation.get().dir('jsapi'))
    }
}

configurations {
    js
}

artifacts {
    js(webpackSourcesLocation) {
        builtBy webpackSources
    }
}
