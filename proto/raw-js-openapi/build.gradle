plugins {
    id 'com.bmuschko.docker-remote-api'
    id 'io.deephaven.project.register'
}

evaluationDependsOn Docker.registryProject('node')

configurations {
    js
}

dependencies {
    js project(path: ':proto:proto-backplane-grpc', configuration: 'js')
}

def webpackSourcesLocation = layout.buildDirectory.dir("${buildDir}/dhapi")

def webpackSources = Docker.registerDockerTask(project, 'webpackSources') {
    copyIn {
        from(configurations.js) {
            // note: we are only copying the JS and not TS files.
            include '**/*.js'
            into 'raw-js-openapi/build/js-src'
        }

        from('src') {
            into 'raw-js-openapi/src'
        }

        from ('webpack.config.js') {
            into 'raw-js-openapi/'
        }
        from ('tsconfig.json') {
            into 'raw-js-openapi/'
        }
        from ('Dockerfile')
        from ('package.json')
        from ('package-lock.json')
        from ('.npmrc')
    }
    parentContainers = [ Docker.registryTask(project, 'node') ]
    imageName = Docker.localImageName('js-out')
    containerOutPath = '/usr/src/app/raw-js-openapi/build/js-out'
    copyOut {
        include 'dh-internal.js'
        into(webpackSourcesLocation.get().dir('jsapi'))
    }
}

artifacts {
    js(webpackSourcesLocation) {
        builtBy webpackSources
    }
}
