import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import io.deephaven.tools.docker.Architecture
import io.deephaven.tools.License

plugins {
    id 'com.bmuschko.docker-remote-api'
    id 'io.deephaven.project.register'
}

evaluationDependsOn Docker.registryProject('alpine')

def dockerLicenses = License.createFrom(project).syncDockerLicense()

def context = project.layout.buildDirectory.dir('docker')

def prepareDocker = project.tasks.register('prepareDocker', Sync) {
    // TODO(deephaven-core#1596): Remove extra dependencies for build-ci.yml
    inputs.files Docker.registryFiles(project, 'alpine')

    from 'src/main/docker'
    from('src/main/arch/') {
        into 'arch'
    }
    from('src/main/common') {
        into 'contents'
    }
    from (dockerLicenses.get().outputs) {
        into 'contents'
    }
    into context
}

def buildDocker = Docker.registerDockerTwoPhaseImage(project, 'grpc-proxy', 'install') {
    inputDir.set context
    inputs.files prepareDocker.get().outputs.files
    inputs.files Docker.registryFiles(project, 'alpine')
    buildArgs.put('DEEPHAVEN_VERSION', project.version)
}

assemble.dependsOn buildDocker