plugins {
    id 'com.bmuschko.docker-remote-api'
    id 'io.deephaven.project.register'
}

evaluationDependsOn Docker.registryProject('python')

configurations {
    compile.extendsFrom fishConfig, jdom
    testCompile.extendsFrom fishBaseTest
    combinedJavadoc
}

dependencies {
    compile project(':engine-table'),
            project(':Plot'),
            project(':extensions-csv'),
            project(':extensions-kafka'),
            project(':extensions-parquet-table')

    Classpaths.inheritGroovy(project, 'groovy', 'implementation')
    Classpaths.inheritGroovy(project, 'groovy-json', 'implementation')

    runtime project(path: ':configs'),
            project(path: ':test-configs'),
            project(':extensions-kafka')

    runtimeOnly project(':log-to-slf4j')
    Classpaths.inheritLogbackClassic(project)

    testRuntime project(path: ':configs'),
            project(path: ':test-configs')

    combinedJavadoc project(path: ':combined-javadoc', targetConfiguration: 'combinedJavadoc')
}

def workspace = "$rootDir/tmp/workspace"
def workDir = "$rootDir/.."
String devRoot = rootDir.absolutePath

task groovyStaticImportGenerator(type: JavaExec, dependsOn: 'classes') {
    description 'Run GroovyStaticImportGenerator'

    main = 'io.deephaven.libs.GroovyStaticImportGenerator'
    args devRoot, 'false'
    classpath = sourceSets.main.runtimeClasspath
    workingDir = workDir
}

task groovyStaticImportGeneratorAssert(type: JavaExec, dependsOn: 'classes') {
    description 'Run GroovyStaticImportGenerator to assert that the generated code has not changed'

    main = 'io.deephaven.libs.GroovyStaticImportGenerator'
    args devRoot, 'true'
    classpath = sourceSets.main.runtimeClasspath
    workingDir = workDir
    onlyIf { System.getenv('NO_ASSERT') != 'true' }

}

task generateAxesPlotMethods(type: JavaExec, dependsOn: 'classes') {
    description 'Run GenerateAxesPlotMethods'

    main = 'io.deephaven.plot.util.GenerateAxesPlotMethods'
    args devRoot, 'false'
    classpath = sourceSets.main.runtimeClasspath
    workingDir = workDir
    systemProperty 'workspace', workspace
    systemProperty 'devroot', devRoot
    systemProperty 'Configuration.rootFile', 'dh-defaults.prop'
}

task generateAxesPlotMethodsAssert(type: JavaExec, dependsOn: 'classes') {
    description 'Run GenerateAxesPlotMethods to assert that the generated code has not changed'

    main = 'io.deephaven.plot.util.GenerateAxesPlotMethods'
    args devRoot, 'true'
    classpath = sourceSets.main.runtimeClasspath
    workingDir = workDir
    systemProperty 'workspace', workspace
    systemProperty 'devroot', devRoot
    systemProperty 'Configuration.rootFile', 'dh-defaults.prop'
    onlyIf { System.getenv('NO_ASSERT') != 'true' }
}

task generateMultiSeries(type: JavaExec, dependsOn: 'classes') {
    description 'Run GenerateMultiSeries'

    main = 'io.deephaven.plot.util.GenerateMultiSeries'
    args devRoot, 'false'
    classpath = sourceSets.main.runtimeClasspath
    workingDir = workDir
    systemProperty 'workspace', workspace
    systemProperty 'devroot', devRoot
    systemProperty 'Configuration.rootFile', 'dh-defaults.prop'
}

task generateMultiSeriesAssert(type: JavaExec, dependsOn: 'classes') {
    description 'Run GenerateMultiSeries to assert that the generated code has not changed'

    main = 'io.deephaven.plot.util.GenerateMultiSeries'
    args devRoot, 'true'
    classpath = sourceSets.main.runtimeClasspath
    workingDir = workDir
    systemProperty 'workspace', workspace
    systemProperty 'devroot', devRoot
    systemProperty 'Configuration.rootFile', 'dh-defaults.prop'
    onlyIf { System.getenv('NO_ASSERT') != 'true' }
}

task generateFigureImmutable(type: JavaExec, dependsOn: 'classes') {
    description 'Run GenerateFigureImmutable'
    dependsOn generateAxesPlotMethods, generateMultiSeries

    main = 'io.deephaven.plot.util.GenerateFigureImmutable'
    args devRoot, 'false'
    classpath = sourceSets.main.runtimeClasspath
    workingDir = workDir
    systemProperty 'workspace', workspace
    systemProperty 'devroot', devRoot
    systemProperty 'Configuration.rootFile', 'dh-defaults.prop'
}

task generateFigureImmutableAssert(type: JavaExec, dependsOn: 'classes') {
    description 'Run GenerateFigureImmutable to assert that the generated code has not changed'
    dependsOn generateAxesPlotMethodsAssert, generateMultiSeriesAssert

    main = 'io.deephaven.plot.util.GenerateFigureImmutable'
    args devRoot, 'true'
    classpath = sourceSets.main.runtimeClasspath
    workingDir = workDir
    systemProperty 'workspace', workspace
    systemProperty 'devroot', devRoot
    systemProperty 'Configuration.rootFile', 'dh-defaults.prop'
    onlyIf { System.getenv('NO_ASSERT') != 'true' }
}

task generatePlottingConvenience(type: JavaExec, dependsOn: 'classes') {
    description 'Run GeneratePlottingConvenience'
    dependsOn generateFigureImmutable

    main = 'io.deephaven.plot.util.GeneratePlottingConvenience'
    args devRoot
    classpath = sourceSets.main.runtimeClasspath
    workingDir = workDir
    systemProperty 'workspace', workspace
    systemProperty 'devroot', devRoot
    systemProperty 'Configuration.rootFile', 'dh-defaults.prop'
}

task generatePlottingConvenienceAssert(type: JavaExec, dependsOn: 'classes') {
    description 'Run GeneratePlottingConvenience to assert that the generated code has not changed'
    dependsOn generateFigureImmutableAssert

    main = 'io.deephaven.plot.util.GeneratePlottingConvenience'
    args devRoot, 'true'
    classpath = sourceSets.main.runtimeClasspath
    workingDir = workDir
    systemProperty 'workspace', workspace
    systemProperty 'devroot', devRoot
    systemProperty 'Configuration.rootFile', 'dh-defaults.prop'
}

TaskProvider<Task> generatePyDoc = Docker.registerDockerTask(project, "generatePyDoc") {
    copyIn {
        from('python') {
            into 'python'
        }

        // TODO move these to a later sync task, so that we detect changes and minimally build images
        // Copy existing generated JSON to where the image can get it - remove if we stop checking in JSON
        from("$devRoot/Integrations/python/deephaven") {
            // allow javadocExtraction.py to verify existing json
            include 'doc'
            // allow manualDocstrings.py to verify existing json
            include 'docCustom'
            into 'out'
        }
        // Depend on generated javadoc so we can generate JSON from it
        from(configurations.combinedJavadoc) {
            into 'javadoc'
        }
    }

    dockerfile {
        from 'deephaven/python:local-build'

        runCommand 'pip3 install beautifulsoup4==4.9.3 lxml==4.6.3'

        copyFile('.', '.')

        runCommand '''set -eux; \\
                      python3 /python/javadocExtraction.py / false; \\
                      python3 /python/manualDocstrings.py / false
                      '''
    }

    parentContainers = [ Docker.registryTask(project, 'python') ]
    imageName = 'deephaven/pydocs:local-build'
    copyOut {
        into("$devRoot/Integrations/python/deephaven")
        preserve {// only overwrite doc and docCustom directories
            include '**/*'
            exclude 'doc'
            exclude 'docCustom'
        }
    }
}

tasks.register("generateManPyDocAssert") {
    doLast {
        logger.warn "This task no longer checks output, we're expecting to always regenerate now"
    }
}
tasks.register("generatePyDocAssert") {
    doLast {
        logger.warn "This task no longer checks output, we're expecting to always regenerate now"
    }
}

def pythonStaticMethodsArgs = ['io.deephaven.time.calendar.Calendars',
                                    '/Integrations/python/deephaven/Calendars/__init__.py',
                               'io.deephaven.time.DateTimeUtils',
                                    '/Generators/src/main/java/io/deephaven/pythonPreambles/DateTimeUtilsPreamble.txt',
                                    '/Integrations/python/deephaven/DateTimeUtils/__init__.py',
                               'io.deephaven.parquet.table.ParquetTools',
                                    '/Generators/src/main/java/io/deephaven/pythonPreambles/ParquetToolsPreamble.txt',
                                    '/Integrations/python/deephaven/ParquetTools/__init__.py',
                               'io.deephaven.engine.util.TableTools',
                                    '/Generators/src/main/java/io/deephaven/pythonPreambles/TableToolsPreamble.txt',
                                    '/Integrations/python/deephaven/TableTools/__init__.py',
                               'io.deephaven.plot.colors.ColorMaps',
                                   '/Integrations/python/deephaven/Plot/ColorMaps/__init__.py',
                               'io.deephaven.engine.util.WindowCheck',
                                    '/Integrations/python/deephaven/TableManipulation/WindowCheck.py',
                               'io.deephaven.engine.table.impl.util.TableLoggers',
                                    '/Integrations/python/deephaven/TableLoggers/__init__.py',
                               'io.deephaven.engine.table.impl.util.PerformanceQueries',
                                    '/Integrations/python/deephaven/PerformanceQueries/__init__.py']

task generatePythonIntegrationStaticMethods(type: JavaExec, dependsOn: 'classes') {
    description 'Run GeneratePythonIntegrationStaticMethods'

    main = 'io.deephaven.python.PythonStaticGenerator'
    args ([devRoot, 'false'] + pythonStaticMethodsArgs) as String[]
    classpath = sourceSets.main.runtimeClasspath
    workingDir = workDir
    systemProperty 'workspace', workspace
    systemProperty 'devroot', devRoot
    systemProperty 'Configuration.rootFile', 'dh-defaults.prop'
    dependsOn generatePyDoc
}

task generatePythonIntegrationStaticMethodsAssert(type: JavaExec, dependsOn: 'classes') {
    description 'Run GeneratePythonIntegrationStaticMethods'

    main = 'io.deephaven.python.PythonStaticGenerator'
    args ([devRoot, 'true'] + pythonStaticMethodsArgs) as String[]
    classpath = sourceSets.main.runtimeClasspath
    workingDir = workDir
    systemProperty 'workspace', workspace
    systemProperty 'devroot', devRoot
    systemProperty 'Configuration.rootFile', 'dh-defaults.prop'
    onlyIf { System.getenv('NO_ASSERT') != 'true' }
}

task generatePythonFigureWrapper(type: JavaExec, dependsOn: 'classes') {
    description 'Run GeneratePythonFigureWrapper'
    dependsOn generatePlottingConvenience

    main = 'io.deephaven.python.PythonPlottingGenerator'
    args devRoot, 'false'
    classpath = sourceSets.main.runtimeClasspath
    workingDir = workDir
    systemProperty 'workspace', workspace
    systemProperty 'devroot', devRoot
    systemProperty 'Configuration.rootFile', 'dh-defaults.prop'
    dependsOn generatePyDoc, ':Generators:generateFigureImmutable'
}

task generatePythonFigureWrapperAssert(type: JavaExec, dependsOn: 'classes') {
    description 'Run GeneratePythonFigureWrapper'
    dependsOn generatePlottingConvenienceAssert

    main = 'io.deephaven.python.PythonPlottingGenerator'
    args devRoot, 'true'
    classpath = sourceSets.main.runtimeClasspath
    workingDir = workDir
    systemProperty 'workspace', workspace
    systemProperty 'devroot', devRoot
    systemProperty 'Configuration.rootFile', 'dh-defaults.prop'
    onlyIf { System.getenv('NO_ASSERT') != 'true' }
}

tasks.register 'generateAllPython', {
    Task t -> t.dependsOn ':Generators:generatePythonIntegrationStaticMethods',
            ':Generators:generatePythonFigureWrapper'
}


tasks.register 'generateAllPythonAssert', {
    Task t -> t.dependsOn ':Generators:generatePythonIntegrationStaticMethodsAssert',
            ':Generators:generatePythonFigureWrapperAssert'
}

tasks.register 'generateAll', {
    Task t -> t.dependsOn  ':Generators:groovyStaticImportGenerator',
                           ':Generators:generateAxesPlotMethods',
                           ':Generators:generateMultiSeries',
                           ':Generators:generateFigureImmutable',
                           ':Generators:generatePlottingConvenience',
                           ':Generators:generateAllPython'
    t.group = '~Deephaven'
    t.description = 'Runs all code generators'
}

project.tasks.getByName('quick').dependsOn groovyStaticImportGeneratorAssert,
        generateAxesPlotMethodsAssert,
        generateMultiSeriesAssert,
        generateFigureImmutableAssert,
        generatePlottingConvenienceAssert,
        generateAllPythonAssert
