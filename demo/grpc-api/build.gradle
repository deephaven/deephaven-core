import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerInspectImage
import com.bmuschko.gradle.docker.tasks.image.DockerRemoveImage

plugins {
    id 'com.bmuschko.docker-remote-api'
}

evaluationDependsOn ':grpc-api-server-docker'

dependencies {
    // To be provided by the DH environment
    //compileOnly 'io.deephaven:deephaven-table-api:0.5.0'
    //compileOnly 'io.deephaven:deephaven-qst:0.5.0'
}

def dockerfileDir = project.layout.buildDirectory.dir('prepareDocker')

def prepareDocker = project.tasks.register('prepareDocker', Sync) {
    it.from 'src/main/docker/Dockerfile'
    it.from('src/main/app.d') {
        into 'app.d'
    }
    it.from(configurations.runtimeClasspath) {
        into 'libs'
    }
    it.from(jar) {
        into 'libs'
    }
    it.into dockerfileDir
}

def buildDocker = project.tasks.register('buildDocker', DockerBuildImage) {
    it.dependsOn prepareDocker, project(':grpc-api-server-docker').tasks.findByName('dockerBuildImage')
    it.inputDir.set(dockerfileDir)
    it.images.add((String)project.property('imageId'))
}

project.tasks.register('cleanDocker', DockerRemoveImage) {
    it.doFirst {
        buildDocker.get().imageIdFile.get().getAsFile().delete()
    }
    it.imageId.set project.property('imageId') as String
    it.onError { exception ->
        if (!exception.message.contains('No such image'))
            throw exception
    }
}

assemble.dependsOn buildDocker