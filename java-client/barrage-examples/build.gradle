plugins {
    id 'application'
    id 'io.deephaven.project.register'
}

dependencies {
    implementation project(':java-client-barrage')
    implementation project(':java-client-example-utilities')

    testImplementation libs.assertj
    testImplementation platform(libs.junit.bom)
    testImplementation libs.junit.jupiter

    runtimeOnly project(':log-to-slf4j')
    runtimeOnly libs.logback.classic
}

test {
    useJUnitPlatform()
}

startScripts.enabled = false

def createApplication = { String name, String mainClass ->
    project.tasks.register("create-${name}", CreateStartScripts) {script ->
        script.mainClass.set mainClass
        script.applicationName = name
        script.outputDir = project.layout.buildDirectory.dir(name).get().asFile
        script.classpath = startScripts.classpath
        script.defaultJvmOpts = [
                '-server',
                '-DConfiguration.rootFile=dh-defaults.prop'
        ]
    }
}

application.applicationDistribution.into('bin') {
    from(createApplication('subscribe-table', 'io.deephaven.client.examples.SubscribeTable'))
    from(createApplication('snapshot-table', 'io.deephaven.client.examples.SnapshotTable'))
    fileMode = 0755
}

apply plugin: 'io.deephaven.java-open-nio'
