plugins {
    id 'application'
    id 'io.deephaven.project.register'
}

dependencies {
    implementation project(':java-client-session')
    implementation project(':java-client-example-utilities')

    implementation platform(libs.grpc.bom)
    runtimeOnly libs.grpc.netty

    testImplementation libs.assertj
    testImplementation platform(libs.junit.bom)
    testImplementation libs.junit.jupiter

    runtimeOnly libs.logback.classic
}

test {
    useJUnitPlatform()
}

startScripts.enabled = false

def createApplication = { String name, String mainClass ->
    project.tasks.register("create-${name}", CreateStartScripts) {script ->
        script.mainClass.set mainClass
        script.applicationName = name
        script.outputDir = project.layout.buildDirectory.dir(name).get().asFile
        script.classpath = startScripts.classpath
        script.defaultJvmOpts = [
                '-server',
        ]
    }
}

application.applicationDistribution.into('bin') {
    from(createApplication('publish-tables', 'io.deephaven.client.examples.PublishTables'))
    from(createApplication('write-qsts', 'io.deephaven.client.examples.WriteExampleQsts'))
    from(createApplication('table-manager', 'io.deephaven.client.examples.TableManagerExample'))
    from(createApplication('execute-script', 'io.deephaven.client.examples.ExecuteScript'))
    from(createApplication('execute-code', 'io.deephaven.client.examples.ExecuteCode'))
    from(createApplication('subscribe-fields', 'io.deephaven.client.examples.SubscribeToFields'))
    from(createApplication('connect-check', 'io.deephaven.client.examples.ConnectCheck'))
    from(createApplication('fetch-object', 'io.deephaven.client.examples.FetchObject'))
    from(createApplication('subscribe-to-logs', 'io.deephaven.client.examples.SubscribeToLogs'))
    from(createApplication('publish', 'io.deephaven.client.examples.Publish'))
    from(createApplication('message-stream-send-receive', 'io.deephaven.client.examples.MessageStreamSendReceive'))
    from(createApplication('filter-table', 'io.deephaven.client.examples.FilterTable'))
    from(createApplication('create-shared-id', 'io.deephaven.client.examples.CreateSharedId'))
    from(createApplication('print-configuration-constants', 'io.deephaven.client.examples.PrintConfigurationConstants'))

    fileMode = 0755
}

