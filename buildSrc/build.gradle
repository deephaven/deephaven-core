plugins {
    id 'groovy'
    id 'groovy-gradle-plugin'
}

// This is a simplification of how our normal project uses io.deephaven.java-toolchain-conventions.
// We choose to use Java 21 as the main compiler (same as the normal project); and set a target language level of Java
// 17. The target language level here does not effect the target language level of the normal project, it just dictates
// the Java features that can be used by our buildSrc code, or our buildSrc dependency's code.

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

tasks.withType(GroovyCompile).configureEach {
    options.release = 17
}

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    implementation('de.esoco.gwt:gwt-gradle-plugin:1.2.0') {
        exclude group: 'org.codehaus.groovy'
        because('needed for GwtTools')
    }

    implementation('com.bmuschko:gradle-docker-plugin:9.3.1') {
        because('needed by plugin com.bmuschko.docker-remote-api')
    }

    implementation('com.avast.gradle:gradle-docker-compose-plugin:0.17.12') {
        because('needed by plugin com.avast.gradle.docker-compose')
    }

    implementation('com.diffplug.spotless:spotless-plugin-gradle:8.1.0') {
        because('needed by plugin java-coding-conventions')
    }

    implementation "com.gradleup.shadow:shadow-gradle-plugin:8.3.8"

    implementation('com.vanniktech.maven.publish:com.vanniktech.maven.publish.gradle.plugin:0.35.0') {
        because('needed by plugin java-publishing-conventions')
    }
}
