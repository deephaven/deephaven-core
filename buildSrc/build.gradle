plugins {
    id 'groovy'
    id 'groovy-gradle-plugin'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    implementation('de.esoco.gwt:gwt-gradle-plugin:1.1.1') {
        exclude group: 'org.codehaus.groovy'
        because('needed for GwtTools')
    }

    implementation('com.bmuschko:gradle-docker-plugin:7.1.0') {
        because('needed by plugin com.bmuschko.docker-remote-api')
    }

    implementation('com.avast.gradle:gradle-docker-compose-plugin:0.16.11') {
        because('needed by plugin com.avast.gradle.docker-compose')
    }

    implementation('com.diffplug.spotless:spotless-plugin-gradle:6.12.0') {
        because('needed by plugin java-coding-conventions')
    }

    // TODO(deephaven-core#1997): Remove license-gradle-plugin
    implementation('gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.16.1') {
        because('needed by plugin java-header-conventions')
    }
}
