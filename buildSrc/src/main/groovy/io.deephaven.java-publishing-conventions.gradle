import io.deephaven.project.util.PublishingTools

plugins {
  id 'java'
  id 'signing'
  id 'maven-publish'
  id 'io.deephaven.javadoc-conventions'
}

java {
  withJavadocJar()
  withSourcesJar()
}

// Any published source jar should contain apt-generated sources
// See https://github.com/gradle/gradle/issues/33287
sourceSets.configureEach { sourceSet ->
  if (tasks.names.contains(sourceSet.getSourcesJarTaskName())) {
    tasks.named(sourceSet.getSourcesJarTaskName(), Jar) {
      from sourceSet.output.generatedSourcesDirs
    }
  }
}

tasks.withType(Javadoc) {
  // https://github.com/gradle/gradle/issues/19869
  options.addStringOption('sourcepath', sourceSets.main.allJava.getSourceDirectories().getAsPath())
}

PublishingTools.setupPublications(project) {
  from components.java
}

PublishingTools.setupRepositories(project)
PublishingTools.setupMavenPublication(project, publishing.publications.mavenJava)
PublishingTools.setupSigning(project, publishing.publications.mavenJava)
