import org.gradle.api.tasks.javadoc.Javadoc
import org.gradle.jvm.toolchain.JavaLanguageVersion

tasks.withType(Javadoc) {
    javadocTool.set(javaToolchains.javadocToolFor{javadocSpec ->
        // Javadoc version >=11 is needed for search
        // Javadoc version >12 is needed to avoid javadoc bugs in linking with modules
        javadocSpec.languageVersion = JavaLanguageVersion.of(17)
    }.get())

    options.encoding = 'UTF-8'

    options.links = ['https://docs.oracle.com/en/java/javase/11/docs/api/']
    options.addBooleanOption('Xdoclint:none', true)
    options.tags = ['apiNote', 'implNote', 'note']

    // Don't fail on warnings, let specific projects enable this if desired
//  options.addBooleanOption('Xwerror', true)

    // https://github.com/gradle/gradle/issues/19869
    options.addStringOption('sourcepath', sourceSets.main.allJava.getSourceDirectories().getAsPath())
}
