import io.deephaven.project.util.JavaDependencies
import io.deephaven.project.util.PublishingTools
import io.deephaven.tools.License

plugins {
    id 'io.deephaven.common-conventions'
    id 'java-platform'
    // Note: applying `com.vanniktech.maven.publish.base` instead of `com.vanniktech.maven.publish` which gives us more
    // control over (otherwise `com.vanniktech.maven.publish.JavaPlatform` would be applied by default).
    id 'com.vanniktech.maven.publish.base'
    id 'signing'
}

project
        .extensions
        .getByType(PublishingExtension)
        .publications
        .create('myPublication', MavenPublication) {
            // "javaPlatform" is the component name created by the java-platform plugin (does not provide a constant)
            from(project.components.named('javaPlatform').get())
            pom {
                licenses {
                    license {
                        name = License.APACHE_LICENSE_NAME
                        url = License.APACHE_LICENSE_URL
                    }
                }
            }
        }

PublishingTools.setupBomPublishing(project)

project.tasks
        .getByName('quick')
        .dependsOn JavaDependencies.verifyAllConfigurationsArePublicTask(project)
