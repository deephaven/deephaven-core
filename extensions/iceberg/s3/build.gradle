plugins {
    id 'java-library'
    id 'io.deephaven.project.register'
    id 'io.deephaven.hadoop-common-dependencies'
}

evaluationDependsOn Docker.registryProject('localstack')
evaluationDependsOn Docker.registryProject('minio')

description 'Iceberg: Support to read iceberg catalogs.'

dependencies {
    api project(':extensions-iceberg')

    // Bring in the AWS / S3 extensions
    api platform(libs.iceberg.bom)
    api libs.iceberg.api
    implementation libs.iceberg.core
    implementation libs.iceberg.bundled.guava

    implementation project(':extensions-s3')
    implementation libs.iceberg.aws

    implementation platform(libs.awssdk.bom)
    implementation libs.awssdk.s3
    implementation libs.awssdk.crt.client
    runtimeOnly libs.awssdk.sts
    implementation libs.awssdk.glue

    // We don't want to explicitly pull in dependencies for dynamodb (org.apache.iceberg.aws.dynamodb.DynamoDbCatalog),
    // but we need to be able to compile against it to implement AwsClientFactory
    compileOnly libs.awssdk.dynamodb

    // We don't want to explicitly pull in dependencies for KMS (there doesn't seem to be anything in Iceberg that
    // actually calls it?), but we need to be able to compile against it to implement AwsClientFactory
    compileOnly libs.awssdk.kms

    compileOnly libs.autoservice
    annotationProcessor libs.autoservice.compiler

    testImplementation project(':engine-test-utils')

    testImplementation libs.testcontainers
    testImplementation libs.testcontainers.junit.jupiter
    testImplementation libs.testcontainers.localstack
    testImplementation libs.testcontainers.minio

    testImplementation TestTools.projectDependency(project, 'extensions-s3')
    testImplementation TestTools.projectDependency(project, 'extensions-iceberg')

    testRuntimeOnly project(':test-configs')
    testRuntimeOnly project(':log-to-slf4j')
    testRuntimeOnly libs.slf4j.simple

    testImplementation platform(libs.junit.bom)
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.jupiter.engine
    testRuntimeOnly libs.junit.platform.launcher
}

test {
    useJUnitPlatform {
        excludeTags("testcontainers")
    }
}

tasks.register('testOutOfBand', Test) {
    useJUnitPlatform {
        includeTags("testcontainers")
    }

    dependsOn Docker.registryTask(project, 'localstack')
    systemProperty 'testcontainers.localstack.image', Docker.localImageName('localstack')

    dependsOn Docker.registryTask(project, 'minio')
    systemProperty 'testcontainers.minio.image', Docker.localImageName('minio')
}
