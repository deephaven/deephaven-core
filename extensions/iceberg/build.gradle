plugins {
    id 'java-library'
    id 'io.deephaven.project.register'
    id 'io.deephaven.hadoop-common-dependencies'
}

description 'Iceberg: Support to read iceberg catalogs.'

dependencies {
    api project(':engine-api')
    api project(':engine-table')

    implementation project(':engine-base')
    implementation project(':log-factory')
    implementation project(':Configuration')

    compileOnly libs.autoservice
    annotationProcessor libs.autoservice.compiler

    compileOnly project(':util-immutables')
    annotationProcessor libs.immutables.value

    implementation libs.parquet.hadoop

    implementation project(':extensions-parquet-base')
    implementation project(':extensions-parquet-table')

    api platform(libs.iceberg.bom)
    api libs.iceberg.api
    implementation libs.iceberg.core
    implementation libs.iceberg.bundled.guava

    implementation libs.hadoop.hdfs.client
    // Also inherits hadoop-common, see io.deephaven.hadoop-common-dependencies

    implementation libs.avro

    implementation libs.commons.lang3

    testImplementation libs.assertj

    testImplementation platform(libs.junit.bom)
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.jupiter.engine
    testRuntimeOnly libs.junit.platform.launcher

    testRuntimeOnly project(':test-configs')
    testRuntimeOnly project(':log-to-slf4j')
    testRuntimeOnly libs.slf4j.simple

    testRuntimeOnly libs.xerial.sqlite.jdbc

    // NOTE: we need to pull down more hadoop dependencies,
    // buildSrc/src/main/groovy/io.deephaven.hadoop-common-dependencies.gradle excludes some stuff we need to do
    // Hadoop FileIO.
    testRuntimeOnly libs.hadoop.common

    testImplementation project(':engine-test-utils')
}

/**
 * We are setting up a "security-manage-allow" tag to add the system property `-Djava.security.manager=allow`
 * to the tests that need it for Java 23+. Otherwise, the following exception is thrown:
 *
 * java.lang.UnsupportedOperationException: getSubject is supported only if a security manager is allowed
 *   at java.base/javax.security.auth.Subject.getSubject(Subject.java:347)
 *   at org.apache.hadoop.security.UserGroupInformation.getCurrentUser(UserGroupInformation.java:588)
 *
 * See https://issues.apache.org/jira/browse/HADOOP-19212
 * See https://jdk.java.net/23/release-notes#JDK-8296244
 */

test {
    useJUnitPlatform {
        excludeTags("security-manager-allow")
    }
}

def testSecurityManagerAllow = tasks.register('testSecurityManagerAllow', Test) {
    useJUnitPlatform {
        includeTags("security-manager-allow")
    }
    if (it.javaLauncher.get().metadata.languageVersion >= JavaLanguageVersion.of(23)) {
        it.systemProperty("java.security.manager", "allow")
    }
}

check.dependsOn testSecurityManagerAllow
