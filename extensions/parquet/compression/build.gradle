plugins {
    id 'java-library'
    id 'io.deephaven.project.register'
}

dependencies {
    api project(':Util')

    // TODO(deephaven-core#3148): LZ4_RAW parquet support
    api('org.apache.parquet:parquet-hadoop:1.13.0') {
        because 'Required for Compression Codecs'
    }

    // TODO(deephaven-core#806): Remove dependency on hadoop-common
    api('org.apache.hadoop:hadoop-common:3.3.3') {
        because 'Required for org.apache.hadoop.conf.Configuration'
        // do not take any dependencies of this project,
        // we just want a few classes (Configuration, Path) for
        // simplified prototyping work, and api compatibility.
        transitive = false
        // if we actually need any more of hadoop at runtime,
        // we can add more jars w/ transitive=false,
        // or replace transitive=false here w/ more exclusions;
        // (we want to avoid pulling in netty, loggers, jetty-util, guice and asm).
    }

    implementation project(':Configuration')

    implementation depCommonsIo

    Classpaths.inheritGuava(project)

    runtimeOnly('org.lz4:lz4-java:1.8.0') {
        // also consider lz4-pure-java to avoid native code
        because 'hadoop-common required dependency for LZ4Codec'
    }
    // Pick up default jvm-compatible compression codecs
    implementation('io.airlift:aircompressor:0.24') {
        because 'Provides Lz4, LZO, Zstd compression support for parquet'
    }

    implementation('org.xerial.snappy:snappy-java:1.1.8.4') {
        because 'Provides snappy compression for parquet, with native support for all platforms deephaven works on'
    }
}
