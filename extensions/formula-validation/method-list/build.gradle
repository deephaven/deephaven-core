plugins {
    id 'java-library'
    id 'io.deephaven.project.register'
}

description "Formula validation using lists of pointcuts matched by openrewrite."

dependencies {
    shadow project(':Util')
    shadow project(':engine-validation')
    implementation libs.rewrite.java

    testImplementation project(":engine-test-utils")
    testImplementation project(":extensions-validation-base")

    testRuntimeOnly project(':log-to-slf4j')
    testRuntimeOnly project(':configs')
    testRuntimeOnly project(':test-configs')
    testRuntimeOnly libs.slf4j.simple
}

// Relocate the openrewrite dependencies into a minimized fatjar to avoid potential conflicts.
shadowJar {
    for (String pattern : ["com", "de", "google", "groovy", "nonapi", "net", "org"]) {
        relocate pattern, 'io.deephaven.server.table.validation.methodlist.rebased.' + pattern
    }
    relocate('io', 'io.deephaven.server.table.validation.methodlist.rebased.io') {
        exclude 'io.deephaven.**'
    }

    mergeServiceFiles()
    zip64 = true
    minimize()
}