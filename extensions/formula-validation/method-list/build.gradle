plugins {
    id 'java-library'
    id 'io.deephaven.project.register'
}

description "Formula validation using lists of pointcuts matched by openrewrite."

dependencies {
    shadow project(':Util')
    shadow project(':engine-validation')
    implementation libs.rewrite.java
    testImplementation project(":engine-test-utils")
    testImplementation project(":server")
    testImplementation project(':authorization')
    testImplementation project(':extensions-validation-base')
    testImplementation libs.commons.lang3
    testImplementation libs.javaparser.core

    testRuntimeOnly project(':log-to-slf4j')
    testRuntimeOnly project(':configs')
    testRuntimeOnly project(':test-configs')
    testRuntimeOnly libs.slf4j.simple
}

// Relocate the openrewrite dependencies into a minimized fatjar to avoid potential conflicts.
shadowJar {
    for (String pattern : ["com", "de", "google", "groovy", "io", "nonapi", "net", "org"]) {
        relocate pattern, 'io.deephaven.server.table.validation.methodlist.rebased.' + pattern
    }
    mergeServiceFiles()
    zip64 = true
    minimize()
}